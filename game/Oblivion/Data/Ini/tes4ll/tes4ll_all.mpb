[tes4ll]
@logfile LogFile -filename=$logfile

@makepedestals SetFlag -name="createpedestals" -value="-createpedestals"
@be_silent     SetFlag -name="silent" -value="-silent"
@!back_red   SetFlag -name="back_red"   -value="0.38"
@!back_green SetFlag -name="back_green" -value="0.37"
@!back_blue  SetFlag -name="back_blue"  -value="0.27"



; ============================
; Optional 16 bit matrices (no 32-bit floats)
; enable this only if the tes4ll task exceeds 3GB 
; and you have a crash
; SetFlag -name="use16bit" -value="-use16bit"
@use16bitopt SetFlag -name="use16bit" -value="-use16bit"
; ============================

ParseModList -cd    ;reads mod list from Plugins.txt (or from -l option) and set the flags

@!_tmpdir SetFlag -name="_tmpdir" -value="$_gamedir\\Data\\tes4ll.tmp"
SetFlag -name=_tmpdir -value="$_tmpdir" -replace="\\" -with="\\\\" -num=99 $silent
SetFlag -name="_quad_levels" -value=2

SetFlag -name="dds_format" -value="DXT1"
@c_dxt3 SetFlag -name="dds_format" -value="DXT3"
@c_dxt5 SetFlag -name="dds_format" -value="DXT5"

@!minheight SetFlag -name="minheight" -value="-2048"
SetFlag -name="_defaultheight" -value=$minheight

; ****************************
; Make color maps
; ****************************

@tes4qlod_cache {
	SetFlag -name="_install_dir" -value="$_gamedir\Data\"
	FileIterator -pattern="*.bsa" {
		BsaIterator -bsafile=$_filename -regex="textures\\\\landscape\\\\.*\\.dds" { 
			ImportMapFromDDS -even -name=test -replacemap 
			ScaleMap -sizeX=4 -sizeY=4 -map=test 
			SetFlag -name=newname -value=$_bsafilename -replace=".dds" -with=".bmp"
			SetFlag -name=newname -value=$newname -replace="textures\\landscape" -with="tes4qlod_tex\\Oblivion" -num=1
			MkDir -path=$newname -hasfilename
			ExportMap -filename=$newname -map=test
		}
	}

	{ 
		FileIterator -directory="textures\\landscape" -pattern="*.dds" -recursive {
			ImportMapFromDDS -filename=$_filename -even -name=test -replacemap
			ScaleMap -sizeX=4 -sizeY=4 -map=test 
			SetFlag -name=newname -value=$_filename -replace=".dds" -with=".bmp"
			SetFlag -name=newname -value=$newname -replace="textures\\landscape" -with="tes4qlod_tex\\Oblivion" -num=1
			MkDir -path=$newname -hasfilename
			ExportMap -filename=$newname -map=test
		}
	}
}

@!makemeshes @!writeout @!tes4qlod @!makenormalmaps Exit

SetFlag -name=tes4qlod_flag_a -value="" 
@option_blending SetFlag -name=tes4qlod_flag_a -value="-a"

SetFlag -name=tes4qlod_flag_q -value="1"
@qlod2 SetFlag -name=tes4qlod_flag_q -value="2"
@qlod4 SetFlag -name=tes4qlod_flag_q -value="4"

ImportMapFromModList
SetFlag -name="ws_filename"  -value="$_worldspaceid"
@!_modname SetFlag -name="_modname" -value=""
@new_naming_scheme SetFlag -name="ws_filename" -value="$_worldspace"

SetFlag -name="large_ws" -value=$($_nquads>100) -zerosuppression
@large_ws SetFlag -name="use16bit" -value="-use16bit"

SelectAll

@tes4qlod {
	SetFlag -name="option_silent_opt" -value=""
	@option_silent SetFlag -name="option_silent_opt" -value="-silent"
	SetFlag -name="width_x" -value=$(($_x2-$_x1)*$tes4qlod_flag_q/128)
	SetFlag -name="width_y" -value=$(($_y2-$_y1)*$tes4qlod_flag_q/128)
	CreateMap -name=terraincolor -widthx=$width_x -widthy=$width_y -x1=$_x1 -y1=$_y1 -x2=$_x2 -y2=$_y2 -zscale=1 -rgb -even
	CreateAlgCollection -name=red $silent
	AlgConst -alg=red -add="$back_red" $silent
	CreateAlgCollection -name=green $silent
	AlgConst -alg=green -add="$back_green" $silent
	CreateAlgCollection -name=blue $silent
	AlgConst -alg=blue -add="$back_blue" $silent
	FillColorMap -map=terraincolor  -algblue=blue -alggreen=green -algred=red
	Tes4qlod -colormap=terraincolor -q=$tes4qlod_flag_q $tes4qlod_flag_a $option_silent_opt
	@qlod2 Filter -n=1 -map=terraincolor -rgb
	@make_rocky_filter {
		MakeDerivatives -map="_heightmap" $use16bit			
		@rocky_filter_value  SetFlag -name=cap -value="$(1/$rocky_filter_value)"
		@!rocky_filter_value SetFlag -name=cap -value="1.2"
		SetFlag -name=max_mixing -value="$($rocky_filter_value_mix * $cap)"
		CreateAlgCollection -name=red_filter
		AlgEquation -alg=red_filter   -map=terraincolor -red   -equation="z"
		AlgEquation -alg=red_filter   -map=_heightmap_d2x   -equation="(1-z/$cap)" -zmax=$max_mixing -zmin=0
		AlgEquation -alg=red_filter   -map=_heightmap_d2y   -equation="(1-z/$cap)" -zmax=$max_mixing -zmin=0
		CreateAlgCollection -name=blue_filter
		AlgEquation -alg=blue_filter  -map=terraincolor -blue  -equation="z"
		AlgEquation -alg=blue_filter  -map=_heightmap_d2x -equation="(1-z/$cap)" -zmax=$max_mixing -zmin=0
		AlgEquation -alg=blue_filter  -map=_heightmap_d2y -equation="(1-z/$cap)" -zmax=$max_mixing -zmin=0
		CreateAlgCollection -name=green_filter
		AlgEquation -alg=green_filter -map=terraincolor -green -equation="z"
		AlgEquation -alg=green_filter -map=_heightmap_d2x  -equation="(1-z/$cap)" -zmax=$max_mixing -zmin=0
		AlgEquation -alg=green_filter -map=_heightmap_d2y  -equation="(1-z/$cap)" -zmax=$max_mixing -zmin=0
		FillColorMap -map=terraincolor -algblue=blue_filter -alggreen=green_filter -algred=red_filter
	}
	@make_color_filter {
		CreateAlgCollection -name=red_filter2
		AlgEquation -alg=red_filter2   -map=terraincolor -red   -equation="z*$red_filter_value"
		CreateAlgCollection -name=blue_filter2
		AlgEquation -alg=blue_filter2  -map=terraincolor -blue  -equation="z*$blue_filter_value"
		CreateAlgCollection -name=green_filter2
		AlgEquation -alg=green_filter2 -map=terraincolor -green -equation="z*$green_filter_value"
		FillColorMap -map=terraincolor -algblue=blue_filter2 -alggreen=green_filter2 -algred=red_filter2
	}
	SetFlag -name="_install_dir" -value="$_tmpdir\"
	MkDir -path=$_install_dir
	@option_map ExportMap -map=terraincolor -filename="$_worldspace&Map.bmp" -FlipY
	@overwritelods                    SetFlag -name="_install_dir" -value="$_gamedir\Data\Textures\landscapelod\generated"
	@overwritelods @new_naming_scheme SetFlag -name="_install_dir" -value="$_gamedir\Data\Textures\landscapelod\generated\$_modname"
	MkDir -path=$_install_dir
	SetFlag -name=_flag_format -value="02.0" 
	QuadIterator -level=0 {		
		@!c_none ExportMapToDDS -map=terraincolor -filename="$ws_filename.$($_quad_x * 32).$(32 * $_quad_y).32.dds" -format=$dds_format
		@c_none  ExportMap      -map=terraincolor -filename="$ws_filename.$($_quad_x * 32).$(32 * $_quad_y).32.bmp" 
	}
}

; ****************************
; Make normal maps
; ****************************

@makenormalmaps {
	SelectAll
	CopyMap -name="_heightmap_tmp"
	@2048  ScaleMap -map="_heightmap_tmp" -factor=2
	@512   ScaleMap -map="_heightmap_tmp" -factor=0.5
	@!lodshadows CreateNormalMap -map="_heightmap_tmp" -name="_normalmap" -boost=$z_val
	@lodshadows  CreateNormalMap -map="_heightmap_tmp" -name="_normalmap" -shadows -northboost=$z_val
	SetFlag -name="_install_dir" -value="$_tmpdir\"
	@option_map ExportMap -map=_normalmap -filename="$_worldspace&Normalmap.bmp" -FlipY
	@overwritelods                    SetFlag -name="_install_dir" -value="$_gamedir\Data\Textures\landscapelod\generated"
	@overwritelods @new_naming_scheme SetFlag -name="_install_dir" -value="$_gamedir\Data\Textures\landscapelod\generated\$_modname"
	MkDir -path=$_install_dir
	SetFlag -name=_flag_format -value="02.0" 
	QuadIterator -level=0 {
		@!c_none ExportMapToDDS -map=_normalmap -filename="$ws_filename.$($_quad_x * 32).$(32 * $_quad_y).32_fn.dds" -format=$dds_format
		@c_none  ExportMap      -map=_normalmap -filename="$ws_filename.$($_quad_x * 32).$(32 * $_quad_y).32_fn.bmp" 
	}
}

; ****************************
; Generate optional heightmap
; ****************************

@writeout {
	SetFlag -name="_install_dir" -value="$_tmpdir\"
	SelectAll
	ExportMap -depth=32 -filename="$_worldspace.bmp" 
}

; ****************************
; Make meshes 
; ****************************

@!makemeshes Exit

SetHeight -min -z=$minheight -usegameunits

@!overwritelods                   SetFlag -name="_install_dir" -value="$_tmpdir\"
@overwritelods                    SetFlag -name="_install_dir" -value="$_gamedir\Data\Meshes\Landscape\LOD"
@overwritelods @new_naming_scheme SetFlag -name="_install_dir" -value="$_gamedir\Data\Meshes\Landscape\LOD\$_modname"
MkDir -path=$_install_dir

SetFlag -name="opt_useshapes" -value=""
@ultimate      SetFlag -name="opt_useshapes" -value="-useshapes"
@useshapes     SetFlag -name="opt_useshapes" -value="-useshapes"

; ============================
; Step 1:
; Set single custom points
; With the console command "tdt" coordinates of critical points 
; can be obtained and added in this section:
; ============================
SetFlag -name="_mindistance" -value=128 
@usedatafiles ReadDataFile -filename=ini\tes4ll\RumareCoastLine.dat
@usedatafiles ReadDataFile -filename=ini\tes4ll\WeyeNorth_FortEmpire.dat
@usedatafiles ReadDataFile -filename=ini\tes4ll\WeyeWaynet.dat
@usedatafiles ReadDataFile -filename=ini\tes4ll\Weye_FortAsh.dat
@usedatafiles ReadDataFile -filename=ini\tes4ll\FortEmpire_Aleswell.dat
@usedatafiles ReadDataFile -filename=ini\tes4ll\WayToCheydinhal.dat
@usedatafiles ReadDataFile -filename=ini\tes4ll\FortUrasekToBridge.dat
@usedatafiles ReadDataFile -filename=ini\tes4ll\FortUrasekToAleswell.dat

@xulRollingHills_EV.esp @usedatafiles ReadDataFile -filename=ini\tes4ll\UL_RollingHills.dat
; ============================


; ============================
; Step 2:
; Set cell grid nodes (optional)
; NB: the DivideGrid commands sets vertices at the grid points anyhow.
; So it does not hurt to have the points in advance
; ============================
SelectAll                       ; use the entire worldspace
SetGrid -x=4096 -y=4096
@wallremover @ultimate SetFlag -name="_mindistance" -value=100  
@wallremover @ultimate @!small_world SetAtGridLine -x=4096 -y=4096 -max=80  -zmin=-200
@wallremover @ultimate @small_world  SetAtGridLine -x=4096 -y=4096 -max=50  -zmin=0
@wallremover @high_res SetFlag -name="_mindistance" -value=128 
@wallremover @high_res @!small_world SetAtGridLine -x=4096 -y=4096 -max=150 -zmin=-200
@wallremover @high_res @small_world  SetAtGridLine -x=4096 -y=4096 -max=130 -zmin=0
; ============================


; ============================
; Step 3:
; Set custom panorama view for better horizon
SelectAll
SetFlag -name="_mindistance" -value=256  
@usepanorama Panorama -x=0 -y=0            ;imperial city  
@usepanorama Panorama -x=38520 -y=46880    ;university
@ElsweyrAnequina.esp @usepanorama Panorama -x=7410  -y=-157980    ;corinthe
; ============================


; ============================
; Step 4:
; Improve the coast line (optional)
; Comment the following lines, if the contour is not
; wanted.
; ============================
SelectAll
SetFlag -name="_mindistance" -value=128 

@contour {
	@!contour_grid SetFlag -name="contour_grid" -value=4
	ContourLine -x=4096 -y=4096 -z=0 -offsetx=0 -offsety=0             
	ContourLine -x=512 -y=512 -z=100 -offsetx=0 -offsety=0 -findmax -minmaxgrid=$contour_grid          
	ContourLine -x=512 -y=512 -z=-100 -offsetx=256 -offsety=256 -findmin -minmaxgrid=$contour_grid  
}
         
SetFlag -name="_mindistance" -value=256 ;;; CS uses 1/16th of a cell = 256
; ============================



; ============================
; Step 5: 
; Smooth the heighmap
; Avoids that the random points are triggered by local fluctuations
; ============================
@!small_world Filter -n=4    
@small_world Filter -n=3

MakeDerivatives -map="_heightmap_filtered" $use16bit
; ============================


; ============================
; Step 6:
; Define the density algorithms for the 
; random vertex points in Step 6
; ============================
SelectAll

;AlgConst
AlgFirstOrder -add=0.2
@!small_world AlgSecondOrder -add=0.5 
@small_world AlgSecondOrder -add=1

;improve mountain peaks
;;;@nicer_mountains @!small_world AlgPeakFinder -multiply=1 -radius=4096 -scanradius=8192 -lowest=4000 -insideval=1 -outsideval=0.2
@nicer_mountains AlgPeakFinder -multiply=1 -radius=4096 -scanradius=4096 -lowest=1000 -insideval=1 -outsideval=0.5 -linear

;AlgSecondOrder -multiply=1

; Switch off sampling below the shore line:
AlgLayer -multiply=1 -zmin=-999999 -zmax=-300 -outsideval=1 -insideval=0
; ============================


; ============================
; Step 7:
; Set the random points
; 
; Very important: the Oblivion game engine has large
; limitations. At the end, the mesh of each quad must
; not have a number of vertices, triangles and points larger then 65535
;
; The focus command can be used, as described in Step 6,
; ============================
SelectAll

;;mid-res:
SetFlag -name="_mindistance" -value=256  
SetFlag -name="n" -value=10000   

@high_res {
	SetFlag -name="_mindistance" -value=200  
	@!small_world SetFlag -name="n" -value=13000  
	@small_world  SetFlag -name="n" -value=20000  
}
@ultimate {
	SetFlag -name="_mindistance" -value=200
	@small_world  SetFlag -name="_mindistance" -value=128  
	@!small_world SetFlag -name="n" -value=18000  
	@small_world @_worldspace=DibellasWatch  SetFlag -name="n" -value=30000   
	@small_world @!_worldspace=DibellasWatch SetFlag -name="n" -value=25000
}

QuadIterator -level=0 {
	Echo -text="Filling quad $_quad_x&,$_quad_y with vertices"
	SetAlgVertices -n=$n -max
}
; ============================



; ============================
SelectAll
MakeTriangulation

SelectAll
SplitAtGrid -x=4096 -y=4096
; ============================


; ============================

SetFlag -name=_flag_format -value="02.0" 

QuadIterator -level=0 {
	ExportMeshToNif -filename="$ws_filename.$($_quad_x * 32).$(32 * $_quad_y).32.nif" $opt_useshapes $createpedestals
}

; ============================

@end

; ****************************
[mpgui]
; ****************************

GUIRequestVersion -value="1.22"

@!tes4ll_doonce GUITab -help="Options for mesh generation" -text="LOD Meshes" -name=tes4ll_gui
@!tes4ll_doonce GUITab -help="Normalmaps" -text="Normal Maps" -name=tes4llnm_gui
@!tes4ll_doonce GUITab -help="Options for Lightwave's TES4qLOD" -name="tes4qlod_gui" -text="Color Maps" 
@!tes4ll_doonce GUITab -help="Start process here" -name="final_gui" -text="Start Process" 

@!tes4ll_doonce GUIConsoleEcho -text="Tes4ll batch file for Tamriel loaded"
SetFlag -name=tes4ll_doonce -hidden

SetOption -noskipinfo ;avoid nasty skip messenges in loop mode

; ****************************
[final_gui]
; ****************************

## makes sure that tab is disabled if no worldspace is selected:
GUIEnable -name=final_gui
@!_worldspace GUIDisable -name=final_gui

GUITextBox -text="If you have selected all previous options, you can continue here..." -name=final_gui_intro -vdist=10


GUITextBox -text="General options" -name=tes4ll_text2 -vdist=10

## A checkbox for a basic question:
GUICheckBox -name=overwritelods -text="Overwrite the original files (please make a backup before checking this option)" -help="You have to check this option if tes4ll should place the new files in the LOD directories."
GUIEnable -name=overwritelods 
@!makemeshes @!makenormalmaps @!tes4qlod GUIDisable -name=overwritelods 

GUITextBox -text="Temporary directory" -name=tmpdir_intro -width=0.30 -vdist=5 
GUITextBox -text="$_gamedir\tes4ll.tmp" -name=_tmpdir -input -sameline -width=0.70 -help="Alternative installation path and path for full maps"
GUIEnable -name=_tmpdir
@!makemeshes GUIDisable -name=_tmpdir 

GUICheckBox -name=new_naming_scheme -text="Use the new naming scheme for EngineBugFixes" -help="Use the new naming scheme of EngineBugFixes which contains the name instead of the form ID."
GUIEnable -name=new_naming_scheme 
@!makemeshes @!makenormalmaps @!tes4qlod GUIDisable -name=new_naming_scheme 

GUICheckBox -name=use16bitopt -text="Use 16 bit derivative matrices" -help="Only slightly more unprecise, but saves 2x memory. Use this for large worlds like Onras heightmaps of Tamriel"
GUIEnable -name=use16bitopt 
@!makemeshes GUIDisable -name=use16bitopt

GUITextBox -text="Minimum height" -name=min_height_intro -width=0.30 -vdist=10 
GUITextBox -text="-2048" -name=minheight -input -sameline -width=0.70 -help="Set a mimimum value for the heightmap, in particular for empty cells. Change this only if you know what you are doing."

GUICheckBox -name=writeout -text="Write heightmap as a 32-bit bmp file" -vdist=5

GUITextBox -text="Tool options" -name=tes4ll_text4 -vdist=10

GUIDropDown -name=flagnm_compression -vdist=5 -help="dds compression format."
GUIDropDownItem -parent=flagnm_compression -name=c_dxt1 -text="Use DDS DXT1 compression" 
GUIDropDownItem -parent=flagnm_compression -name=c_dxt3 -text="Use DDS DXT3 compression" 
GUIDropDownItem -parent=flagnm_compression -name=c_dxt5 -text="Use DDS DXT5 compression" -select
GUIDropDownItem -parent=flagnm_compression -name=c_none -text="<None> (needs other tool to convert bmp to dds)" 
GUIEnable -name=flagnm_compression
@!makenormalmaps @!tes4qlod GUIDisable -name=flagnm_compression



## The final button. Once the button is clicked, the flag "exec_tes4ll" is set for one single run of this section
GUIButton -name=exec_tes4ll -text="Click me if you are ready" -vdist=30
GUIEnable -name=exec_tes4ll
@!makenormalmaps @!tes4qlod @!tes4qlod_cache @!makemeshes @!writeout GUIDisable -name=exec_tes4ll


@exec_tes4ll @!overwritelods GUIMessageBox -title="Hint" -text="You have not selected the option to overwrite the original files. Do not forget to copy your files by hand to the appropriate place (or stop the process, and check the \"overwrite\" option)"
@exec_tes4ll @_worldspace GUIExec -exe="tes4ll.exe -f \"$_flaglist\" -w $_worldspace -l \"$_modlist\" stdin" -dumpbatch


; ****************************
[tes4ll_gui]
; ****************************

## makes sure that tab is disabled if no worldspace is selected:
GUIEnable -name=tes4ll_gui
@!_worldspace GUIDisable -name=tes4ll_gui 

GUITextBox -text="Select options for LOD mesh generation..." -name=tes4ll_gui_intro -vdist=10

## A checkbox for a basic question:
GUICheckBox -name=makemeshes -text="Make LOD meshes" -help="Check this if you want to create the LOD meshes" -select

GUITextBox -text="Options for vertices placement" -name=tes4ll_text1 -vdist=10

## A checkbox for a basic question:
GUICheckBox -name=nicer_mountains -text="Enable peak finder algorithm" -help="Enhances the quality of the mountains" -select
GUIEnable -name=nicer_mountains 
@!makemeshes GUIDisable -name=nicer_mountains 

## A checkbox for a basic question:
GUICheckBox -name=wallremover -text="Enable the cell grid boundary wall remover" -help="Enhances the near to far grid transition. Highly recommended" -select
GUIEnable -name=wallremover 
@!makemeshes GUIDisable -name=wallremover 

## A checkbox for a basic question:
GUICheckBox -name=contour -text="Enable the contour algorithm for coasts" -help="Enhances the coast line. Highly recommended" -select -width=0.30
GUIEnable -name=contour 
@!makemeshes GUIDisable -name=contour 
## Add a drop down menu, first the parent:
GUIDropDown -name=contour_grid_s -help="Select your resolution"  -width=0.70 -sameline
## and now the items:
GUIDropDownItem -parent=contour_grid_s -name=c3 -text="Coarse grid" 
GUIDropDownItem -parent=contour_grid_s -name=c2 -text="Normal grid" -select
GUIDropDownItem -parent=contour_grid_s -name=c1 -text="Fine grid" 
GUIEnable -name=contour_grid_s
@!makemeshes GUIDisable -name=contour_grid_s
@c1 SetFlag -name=contour_grid -value="1"
@c2 SetFlag -name=contour_grid -value="4"
@c3 SetFlag -name=contour_grid -value="16"

##
GUICheckBox -name=usedatafiles -text="Use Tamriel data files" -help="Use some hand-placed vertex points" -select
GUIEnable -name=usedatafiles 
@!_worldspace=Tamriel GUIDisable -name=usedatafiles -unselect
@!_worldspace GUIEnable -name=usedatafiles -select
@!makemeshes GUIDisable -name=usedatafiles 

##
GUICheckBox -name=usepanorama -text="Use Tamriel panorama points" -select
GUIEnable -name=usepanorama
@!_worldspace=Tamriel GUIDisable -name=usepanorama -unselect
@!_worldspace GUIEnable -name=usepanorama -select
@!makemeshes GUIDisable -name=usepanorama 

GUITextBox -text="Total resolution (number of vertex points per quad)" -name=tes4ll_text1 -vdist=10

## Add a drop down menu, first the parent:
GUIDropDown -name=flag_res -vdist=5 -help="Select your resolution"
## and now the items:
GUIDropDownItem -parent=flag_res -name=mid_res -text="Mid Resolution " -select
GUIDropDownItem -parent=flag_res -name=high_res -text="High Resolution" 
GUIDropDownItem -parent=flag_res -name=ultimate -text="Ultimate Resolution" 
GUIEnable -name=flag_res 
@!makemeshes GUIDisable -name=flag_res 


GUITextBox -text="Mesh options" -name=tes4ll_text3 -vdist=20

## A checkbox for a basic question:
GUICheckBox -name=useshapes -text="Use shapes instead of strips" -help="Faster generation, but needs more graphics card performance" 
GUIEnable -name=useshapes 
@ultimate GUIDisable -name=useshapes -select
@!makemeshes GUIDisable -name=useshapes 

GUICheckBox -name=small_world -text="Use small world setup" -help="Try to further enhance the settings. Use this option only for small worldspaces which have about 2-4 quads" 
GUIEnable -name=small_world 
@!makemeshes GUIDisable -name=small_world
@_worldspace=Tamriel GUIDisable -name=small_world -unselect


; ****************************
[tes4llnm_gui]
; ****************************

## makes sure that tab is disabled if no worldspace is selected:
GUIEnable -name=tes4llnm_gui
@!_worldspace GUIDisable -name=tes4llnm_gui 

GUITextBox -text="Select options for normal maps generation..." -name=tes4llnm_gui_intro -vdist=10


## A checkbox for a basic question:
GUICheckBox -name=makenormalmaps -text="Make normal maps" -help="Select this if you want to generate the normal maps"

###--------------------------------------------------------------
GUITextBox -text="Graphics options" -name=tes4llnm_text3 -vdist=10

GUIDropDown -name=flagnm_resnormal -vdist=5 -help="Select your texture resolution"
GUIDropDownItem -parent=flagnm_resnormal -name=512 -text="512x512"
GUIDropDownItem -parent=flagnm_resnormal -name=1024 -text="1024x1024" -select
GUIDropDownItem -parent=flagnm_resnormal -name=2048 -text="2048x2048" 
GUIEnable -name=flagnm_resnormal
@!makenormalmaps GUIDisable -name=flagnm_resnormal 

GUICheckBox -name=lodshadows -text="Fake shadows (can take a while)" -help="Tweaks the normap maps such to obtain the impression of landscape shadows" -vdist=5
GUIEnable -name=lodshadows
@!makenormalmaps GUIDisable -name=lodshadows 

GUITextBox -text="z-Boost for non-faked normals" -name=tes4llnm_text3a -vdist=10

GUIDropDown -name=flagnm_zboost -vdist=5 -help="Boost factor for z"
GUIDropDownItem -parent=flagnm_zboost -name=z1 -text="Factor 1 (undistorted)" 
GUIDropDownItem -parent=flagnm_zboost -name=z2 -text="Factor 2 (more contrast)" -select
GUIDropDownItem -parent=flagnm_zboost -name=z4 -text="Factor 4 (even more contrast)"
GUIEnable -name=flagnm_zboost
@lodshadows GUIDisable -name=flagnm_zboost
@!makenormalmaps GUIDisable -name=flagnm_zboost 

@z1 SetFlag -name=z_val -value="1"
@z2 SetFlag -name=z_val -value="2"
@z4 SetFlag -name=z_val -value="4"


; ****************************
[tes4qlod_gui]
; ****************************

## makes sure that tab is disabled if no worldspace is selected:
GUIEnable -name=tes4qlod_gui
@!_worldspace GUIDisable -name=tes4qlod_gui

GUITextBox -text="Select options for color maps generation..." -name=tes4qlod_gui_intro -vdist=10

GUICheckBox -name=tes4qlod_cache -text="Regenerate texture cache" -help="Always required if started first time"

## A checkbox for a basic question:
GUICheckBox -name=tes4qlod -text="Make color maps (calls build-in TES4qLOD)" -help="Select this if you want to generate the color maps with TES4qLOD"

## Just some info:
GUITextBox -text="Please select what TES4qLOD should do" -name=tes4qlod_text -vdist=10

GUITextBox -text="Select resolution per quad:" -name=tes4qlod_text2 -vdist=10

## Add a drop down menu, first the parent:
GUIDropDown -name=tes4qlod_resolution -help="Select the resolution for a single quad (N.B. Vanilla has 512x512)" -vdist=3
## and now the items:
GUIDropDownItem -parent=tes4qlod_resolution -name=qlod1 -text="1024x1024 pixel [recommended]" -select
GUIDropDownItem -parent=tes4qlod_resolution -name=qlod2 -text="2048x2048 pixel" 
GUIDropDownItem -parent=tes4qlod_resolution -name=qlod4 -text="4096x4096 pixel" 
GUIEnable -name=tes4qlod_resolution
@!tes4qlod GUIDisable -name=tes4qlod_resolution



GUITextBox -text="Options:" -name=tes4qlod_text3 -vdist=10

## New blending flag
GUICheckBox -name=option_blending -text="Use layer blending" -help="Uses the opacity of the different layers for alpha blending" -select
GUIEnable -name=option_blending
@!tes4qlod GUIDisable -name=option_blending


## A flag about the total map:
GUICheckBox -name=option_map -text="Create full map of wordspace" -help="Creates a full map of your worldspace" -vdist=5
GUIEnable -name=option_map
@!tes4qlod GUIDisable -name=option_map


## Verbosity option of tes4qlod:
GUICheckBox -name=option_silent -text="Silent TES4qLOD" -help="Disables all the text output of tes4qlod"
GUIEnable -name=option_silent
@!tes4qlod GUIDisable -name=option_silent



GUITextBox -text="Filters:" -name=tes4qlod_text5 -vdist=10

GUICheckBox -name=make_rocky_filter -text="Apply rocky filter, intensity:" -vdist=5 -width=0.20
GUIEnable -name=make_rocky_filter
@!tes4qlod GUIDisable -name=make_rocky_filter

GUITextBox -text="0.8" -name=rocky_filter_value -input -sameline -width=0.80 -help="This is an intensity factor for capping the 2nd derivative. Handle changes with care."
GUIEnable -name=rocky_filter_value
@!make_rocky_filter GUIDisable -name=rocky_filter_value
@!tes4qlod GUIDisable -name=rocky_filter_value

GUICheckBox -name=make_color_filter -text="Apply color filters, intensities:" -vdist=5 -width=0.20
GUIEnable -name=make_color_filter
@!tes4qlod GUIDisable -name=make_color_filter

GUITextBox -name=tes4qlod_text6 -text="Red:" -width=0.10 -sameline
GUIEnable -name=tes4qlod_text6
@!tes4qlod GUIDisable -name=tes4qlod_text6

GUITextBox -text="1.0" -name=red_filter_value -input -sameline -width=0.10 -help="This is an intensity factor for red."
GUIEnable -name=red_filter_value
@!make_color_filter GUIDisable -name=red_filter_value
@!tes4qlod GUIDisable -name=red_filter_value

GUITextBox -name=tes4qlod_text7 -text="Green:" -width=0.10 -sameline
GUIEnable -name=tes4qlod_text7
@!tes4qlod GUIDisable -name=tes4qlod_text7

GUITextBox -text="1.0" -name=green_filter_value -input -sameline -width=0.10 -help="This is an intensity factor for green."
GUIEnable -name=green_filter_value
@!make_color_filter GUIDisable -name=green_filter_value
@!tes4qlod GUIDisable -name=green_filter_value

GUITextBox -name=tes4qlod_text8 -text="Blue:" -width=0.10 -sameline
GUIEnable -name=tes4qlod_text8
@!tes4qlod GUIDisable -name=tes4qlod_text8

GUITextBox -text="1.0" -name=blue_filter_value -input -sameline -width=0.10 -help="This is an intensity factor for blue."
GUIEnable -name=blue_filter_value
@!make_color_filter GUIDisable -name=blue_filter_value
@!tes4qlod GUIDisable -name=blue_filter_value

